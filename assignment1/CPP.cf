-- A program is a sequence of definitions. (1)
DefsProg. Program ::= [Def] ;
terminator Def "" ;
-- A program may also contain comments and preprocessor directives, which are just ignored by the parser
comment "//" ;
comment "#include" ;
comment "#endif" ;
comment "#ifndef" ;
comment "/*" "*/" ;
-- *** Definitions *** --
-- A function definition has a type, a name, an argument list, and a body.
DefFun. Def ::= Type Name "(" [Arg] ")" "{" Body "}" ;
-- A function can optionally be prefixed by inline.
DefInlineFun. Def ::= "inline" Type Name "(" [Arg] ")" "{" Body "}" ;
-- Finally, definitions for using qualified constants are allowed
DefQualifiedConst. Def ::= "using" QualifiedConst ";" ;
-- An argument list is a comma-separated list of argument declarations. It is enclosed in parentheses ( and ).
separator Arg "," ;
MArg. Arg ::= Type Name ;
-- A function body is either a list of statements enclosed in curly brackets { and }
MBody. Body ::= [Stm] ;
terminator Stm "" ;
-- *** Statements *** --
-- Any expression followed by a semicolon ; can be used as a statement. 
ExpStm. Stm ::= Exp ";" ;
-- Any declaration followed by a semicolon ; can be used as a statement.
DecStm. Stm ::= Decl ";" ;
-- Statements returning an expression
RetStm. Stm ::= "return" Exp ";" ;
-- *** Declarations ***
-- Declarations have the same form as argument declarations in functions,
MDecl. Decl ::= Type DeclNames ;

DNElem. DeclNames ::= Name ;
DNCons. DeclNames ::= DeclNames "," Name ;
-- except that they can have more than one variable.

-- *** Expressions *** --
IntExp. Exp2 ::= Integer ;
LitExp. Exp2 ::= Name ;
StrLitExp. Exp2 ::= String ;
QualiConstExp. Exp1 ::= QualifiedConst ;
LeftShiftExp. Exp ::= Exp "<<" Exp ;
RightShiftExp. Exp ::= Exp ">>" Exp ;
coercions Exp 2 ;
-- *** Types *** --
Tint. Type ::= "int" ;
TQualifiedConst. Type ::= QualifiedConst ;

QCElem. QualifiedConst ::= Name ;
QCCond. QualifiedConst ::= QualifiedConst "::" Name ;

token Name (letter (letter | digit | '_')*) ;
